//@version=5
int weeks = input.int(26, "Number of weeks", minval=1)
bool hideCurrentWeek = input(true, "Hide the current week until market close")
bool markExtremes = input(false, "Mark long and short extremes")
indicator("COT Index", shorttitle="COT Index", format=format.percent, precision=0)

import TradingView/LibraryCOT/2 as cot

// Function to fix some symbols.
var string Root_Symbol = syminfo.root
var string CFTC_Code_fixed = cot.convertRootToCOTCode("Auto")
if Root_Symbol == "HG"
    CFTC_Code_fixed := "085692"
else if Root_Symbol == "LBR"
    CFTC_Code_fixed := "058644"

// Function to request COT data for Futures only.
dataRequest(metricName, isLong) =>
    tickerId = cot.COTTickerid("Legacy", CFTC_Code_fixed, false, metricName, isLong ? "Long" : "Short", "All")
    value = request.security(tickerId, "1D", close, ignore_invalid_symbol = true)
    if barstate.islastconfirmedhistory and na(value)
        runtime.error("Could not find relevant COT data based on the current symbol.")
    value


// Function to calculate net long positions.
netLongPositions() =>
    commercialLong = dataRequest("Commercial Positions", true)
    commercialShort = dataRequest("Commercial Positions", false)
    commercialLong - commercialShort

// Function to calculate the Commercials Position Index.
calcIndex() =>
    netPos = netLongPositions()
    minNetPos = ta.lowest(netPos, weeks)
    maxNetPos = ta.highest(netPos, weeks)
    if maxNetPos != minNetPos
        100 * (netPos - minNetPos) / (maxNetPos - minNetPos)
    else
        na

// Calculate the Commercials Position Index.
commercialsIndex = calcIndex()

// Conditional logic based on user input
plotValue = hideCurrentWeek ? (timenow >= time_close ? commercialsIndex : na) : commercialsIndex

// Plot the index and horizontal lines
plot(plotValue, color=color.blue, style=plot.style_line, linewidth=2)
hline(75, "Upper Threshold", color=color.green, linestyle=hline.style_solid, linewidth=1)
hline(25, "Lower Threshold", color=color.red, linestyle=hline.style_solid, linewidth=1)

/// Marking extremes with background color
bgcolor(markExtremes and commercialsIndex >= 75 ? color.new(color.green, 90) : na)
bgcolor(markExtremes and commercialsIndex <= 25 ? color.new(color.red, 90) : na)
